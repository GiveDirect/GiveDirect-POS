apply plugin: 'com.android.application'

buildscript {
    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
    }
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        LuminaUpload {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 27
    defaultConfig {
        vectorDrawables.useSupportLibrary = true
        applicationId "io.givedirect.givedirectpos"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 2
        // {platform}.{sub-system}.{version}.{build}
        // 1.3.X.Y
        versionName "1.3.1.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            javaCompileOptions.annotationProcessorOptions.arguments['butterknife.debuggable'] = 'false'
            signingConfig signingConfigs.LuminaUpload
        }
    }
    productFlavors {
        flavorDimensions 'environment'
        staging {
            dimension 'environment'
            applicationIdSuffix ".staging"
            versionNameSuffix "-staging"
        }
        production {
            dimension 'environment'
            versionNameSuffix "-prod"
            signingConfig signingConfigs.LuminaUpload
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

dependencies {
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation "com.jakewharton.rxrelay2:rxrelay:$rxRelayVersion"
    implementation "com.jakewharton:butterknife:$butterKnifeVersion"
    implementation "com.jakewharton.timber:timber:$timberVersion"
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"
    implementation "android.arch.persistence.room:runtime:$architectureComponentsVersion"
    implementation "android.arch.persistence.room:rxjava2:$architectureComponentsVersion"
    implementation "android.arch.paging:runtime:$pagingComponentVersion"
    implementation "com.jakewharton.threetenabp:threetenabp:$threeTenABPVersion"
    implementation "com.journeyapps:zxing-android-embedded:$zxingEmbeddedVersion"
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    testImplementation "android.arch.persistence.room:testing:$architectureComponentsVersion"
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation 'com.google.code.findbugs:jsr305:3.0.1'
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnifeVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$architectureComponentsVersion"
}
